; 16-3-2021 lextype for the possessive 'one'
; based on basic_n_poss_clitic_lexent but changed to inherit from intr noun instead of nonc-m-nab
; and removed ALTMIN poss_rel
; 29-3-2021 looking at poss_rel, whether it is the one that introduced abtract_q_rels
basic_n_poss_one_lexent := nonc-hm & basic_noun_word &
  [ ALTS.SQPNCT -,
    SYNSEM lex_synsem &
     [ LOCAL [ ARG-S < #spr >,
	       CAT [ HEAD noun &
			  [ ;POSS +,
                            MOD < > ],
                     VAL [ SUBJ < >,
                           SPR < #spr & synsem &
                                 [ --MIN proper_q_rel,
                                   LOCAL [ CAT nomp_cat_min &
                                               [ HEAD [ PRD +,
							CASE non_obliq ],
                                                 VAL [ SUBJ < >,
						       SPCMPS < > ] ],
                                           CONT.HOOK [ LTOP #plbl,
						       INDEX index ],
                                           CONJ cnil ],
                                   NONLOC [ SLASH #slash,
                                            REL #rel,
                                            QUE #que ],
                                   --SIND #pindex & individual_min,
                                   OPT - ] >,
                           COMPS < >,
			   SPCMPS < > ] ],
               CONT.HOOK [ INDEX #index,
			   XARG #pindex ] ],
       LKEYS.ALTKEYREL prep_relation &
             [ PRED poss_rel,
	       LBL #plbl,
               ARG0.E [ TENSE no_tense,
                        ASPECT no_aspect ],
               ARG1 #index & full_ref-ind,
               ARG2 #pindex ],
       NONLOC [ SLASH #slash & 0-dlist,
                REL #rel,
                QUE #que ] ],
       MODIFD notmod ].

; 26-3-2021 removed VAL.SPEC
; RELS PRED changed from def_explicit_q_rel to generic_entity_rel
; also refer to partitive_np_phrase and poss_clitic
; possible: ALTMIN def_explicit_q_rel just like n_-_c-poss_le                
n_poss_one_lexent := basic_n_poss_one_lexent &
  [ SYNSEM lex_synsem &
     [ LOCAL [ CAT [ HEAD.MINORS.MIN norm_nom_rel ],
	       CONT [ HOOK.INDEX #index,
                      RELS <! #keyrel & [ PRED generic_entity_rel,
                                       ARG0 #index,
                                       RSTR #rhand ],
                              #altkeyrel !>,
		      HCONS <! qeq & [ HARG #rhand,
                                       LARG #phand ] !>,
		      ICONS <! !> ] ],
       LKEYS [ KEYREL #keyrel,
               ALTKEYREL #altkeyrel & [ LBL #phand,
					ARG1 #index ] ] ] ].

