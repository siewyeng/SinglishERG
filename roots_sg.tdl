;;; -*- Mode: tdl; Coding: utf-8; -*-
;;;
;;;  Copyright (c) 1994-2018
;;;    Dan Flickinger, Rob Malouf, Emily M. Bender
;;;    see LICENSE for conditions
;;;
;;;  roots.tdl
;;;  Additions for the Singlish branch of ERG

; CSY 20-09-2020 new root for singlish that allows adj as HEAD
;     25-11-2020 added in DIALECT
; 8-3-2020 change HEAD type from non_noun to reduce overgeneration (not sure if undergenerates now)
; 15-12-2021 - remove p from HEAD
root_lepak := phrase &
  [ INFLECTD +,
    SYNSEM basic_root_synsem &
             [ LOCAL [ CAT [ HEAD v_or_a_or_part,
			     MC + ],
			CONJ cnil ] ],
    ORTH root_orth,
    DIALECT sgp ].

; CSY 22-11-2020 a generating version of root_lepak
;		 added GENRE edited to get rid of weird reduplication of pronouns
;     25-11-2020 added in DIALECT
;     19-5-2021 change HEAD from non_noun
; 15-12-2021 - remove p from HEAD
root_gen_lepak := phrase &
  [ INFLECTD +,
    SYNSEM basic_root_phr_synsem &
             [ LOCAL [ CAT [ HEAD v_or_a_or_part &
				[ TAM indic_tam ],
			     MC + ],
		       CONT.ICONS.LIST *nonfocuslist* ],
	       PUNCT.RPUNCT basic_clause_punct ],
    GENRE edited,
    ORTH root_orth,
    DIALECT sgp ].
    
root_non_idiom := sign &
"""
Used to determine on which candidate root edges to not apply the idiom checks
(for efficiency)
"""
  [ IDIOM - ].
