;;; -*- Mode: tdl; Coding: utf-8; -*-
;;;
;;;  Copyright (c) 1994-2018
;;;    Dan Flickinger, Rob Malouf, Emily M. Bender
;;;    see LICENSE for conditions
;;;
;;;  letypes.tdl
;;;
;;;  Lexical entry types (yield of lextypes hierarchy)
;;;
;;;  Created by Dan Flickinger, 15-June-1996
;;;
;;;  $Id: letypes.tdl 7479 2010-02-21 23:11:30Z danf $


; In the following type names, complements marked with "*" in the type names 
; are optional. 

v_-_it_le := main_verb & 
"""
It-subj
<ex>It rains.
"""
  [ SYNSEM atrans_verb ].

v_vp_it_le := main_verb & 
"""
It-subj, Comps: VP (inf)         
<ex>It sufficed to wait.
"""
  [ SYNSEM atrans_inf_verb & 
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT - ] > ] ] ].

v_cp_le := main_verb & 
"""
Cmps S (fin, +/-Q, opt cmplzr)   
<ex>B proved (that) C won.
"""
  [ SYNSEM cp_intrans_verb ].

v_cp_comp_le := main_verb & 
"""
Cmps S (fin, +/-Q, obl cmplzr)   
<ex>B didn't mind that C won.
"""
  [ SYNSEM cp_intrans_verb_comp ].

v_cp_fin-inf_le := main_verb & 
"""
Cmps S (fin/inf, +/-Q)           
<ex>B forgot whether to jump.
"""
  [ SYNSEM cp_fin_inf_intrans_verb ].

v_cp_fin-inf-q_le := main_verb & 
"""
Cmps S (fin/inf, +Q)             
<ex>B wonders whether to jump.
"""
  [ SYNSEM cp_ques_fin_inf_intrans_verb ].

v_cp_fin-inf-q-mc_le := main_verb & 
"""
Cmps S (fin/inf, +Q), allows main clause question complements
<ex>B asks, why is the sky blue?
"""
  [ SYNSEM cp_ques_fin_inf_mc_verb ].

;; DPF 2018-04-29 - Prevent these from undergoing the  CP-frag lexical rule,
;; to avoid apparently spurious ambiguity.
;;
v_cp_prop_le := main_verb & 
"""
Cmps S (fin/inf, -Q, opt cmplzr) 
<ex>B hoped (that) C won. 
"""
  [ SYNSEM cp_prop_intrans_verb,
    ALTS.CPFRAG - ].

v_cp_nfin_le := main_verb & 
"""
Cmps S (head nonfin)             
<ex>Beware that C doesn't win.
"""
  [ SYNSEM cp_prop_intrans_nfin_verb ].

; "find": "I find that Kim has left" but "*I find Kim has left"
v_cp_comp-prop_le := main_verb & 
"""
Cmps S (fin, -Q, obl cmplzr)     
<ex>B resented that C won.
"""
  [ SYNSEM cp_only_prop_intrans_verb ].

; look/seem like
v_cp_like_le := main_verb & 
"""
Cmps S (fin, -!, 'like' cmplzr)  
<ex>B looked like she won.
"""
  [ SYNSEM cp_like_intrans_verb ].

;; inform
v_np-cp_le := main_verb & 
"""
Cmps NP,S (fin, +/-Q)            
<ex>B informed C that D won.
"""
  [ SYNSEM np_trans_cp_verb &
	   [ LOCAL.CAT [ VAL.COMPS.FIRST.OPT - ] ] ].

;; DPF 2016-08-26 - This type is subsumed by the more general 
;; v_cp_fin-inf-q-mc_le which also admits *we asked them what to do*
;; DPF 2017-10-07 - Well, no.  This type takes two complements, an NP and a CP,
;; while v_cp_fin-inf-q-mc_le only takes one complement, a CP.  Sigh.
;; DPF 2020-04-21 - Block cp-frag lexical rule, since it ignores whether the
;; first (NP) complement is obligatory.
;;
; ask
v_np-cp_q_le := main_verb & 
"""
Cmps NP,S (fin, +Q, MC+, inverted)            
<ex>B asked C `who did you admire'
"""
  [ SYNSEM np_trans_cp_ques_verb &
	   [ LOCAL.CAT.VAL.COMPS.FIRST.OPT - ],
    ALTS.CPFRAG - ].

; persuade
v_np-cp_prop_le := main_verb & 
"""
Cmps NP,S (fin, -Q)              
<ex>B persuaded C that D won.
"""
  [ SYNSEM np_trans_cp_prop_verb &
	   [ LOCAL.CAT [ VAL.COMPS.FIRST.OPT - ] ] ].

; tell: tell me whether to stay
v_np-cp_fin-inf_le := main_verb & 
"""
Cmps NP,S (fin/inf, +/-Q)        
<ex>B told C whether to jump.
"""
  [ SYNSEM np_trans_cp_fin_or_inf_verb &
	   [ LOCAL.CAT [ VAL.COMPS.FIRST.OPT - ] ] ].

; ask: asked me whether to stay/whether he should jump
v_np-cp_fin-inf-q_le := main_verb & 
"""
Cmps NP,S (fin/inf, +Q)        
<ex>B asked C whether to jump.
"""
  [ SYNSEM np_trans_cp_fin_or_inf_verb &
	   [ LOCAL.CAT.VAL.COMPS < [ OPT - ], [ --SIND.SF ques ] > ] ].

; ask, prefer 'ask that he be released'
v_cp_sbjnct_le := main_verb & 
"""
Cmps S (bse, subjunctive mood)   
<ex>B required that C be here.
"""
  [ SYNSEM cp_bse_intrans_verb ].

;; Hack for Sherlock, archaic English
; remark 'That be the house, he remarked'
v_cp_sbjnct-xtr_le := main_verb & 
"""
Cmps S (bse, subjunctive mood, extractable)   
<ex>That be the house, he remarked.
"""
  [ SYNSEM cp_bse_intrans_xtra_verb ].

; ask, prefer 'ask that he be released'
v_cp_sbjnct-fin_le := main_verb & 
"""
Cmps S (fin, subjunctive mood)   
<ex>B required that C be here.
"""
  [ SYNSEM cp_sbjnct_intrans_verb ].

; wait
v_cp_inf-only_le := main_verb & 
"""
Cmps S (inf, +/-Q)               
<ex>B waited for C to arrive.
"""
  [ SYNSEM for_to_verb ].

; try
v_vp_seq_le := main_verb & 
"""
Cmps VP, s-equi                  
<ex>B intended to win.
"""
  [ SYNSEM subj_equi_verb ].

; refrain from
v_vp_seq-from_le := main_verb & 
"""
Cmps VP, s-equi, from-VP         
<ex>B refrained from smoking.
"""
  [ SYNSEM subj_equi_from_verb ].

v_prd_seq_le := main_verb_mliszt & 
"""
Cmps Pred-phr, s-equi            
<ex>B remained doubtful.
"""
  [ SYNSEM subj_equi_prd_verb ].

v_prd_seq-idm_le := main_verb_mliszt & 
"""
Cmps Pred-phr, s-equi, idiom     
<ex>B made sure that C won.
"""
  [ SYNSEM subj_equi_prd_adj_verb,
    IDIOM + ].

; run low (on X)
v_prd_seq-va_le := main_verb_mliszt & 
"""
Cmps VP or AP, s-equi             
<ex>B became frustrated.
"""
  [ SYNSEM subj_equi_prd_v_a_verb ].

; run low (on X)
v_ap_seq_le := main_verb_mliszt & 
"""
Cmps Adj-phr, s-equi             
<ex>B proved competent.
"""
  [ SYNSEM subj_equi_prd_adj_verb ].

; want into
v_pp_seq_le := main_verb_mliszt & 
"""
Cmps PP, s-equi                  
<ex>B wanted into the game.
"""
  [ SYNSEM subj_equi_prd_prep_verb ].

v_pp_seq-e_le := main_verb_mliszt & 
"""
Cmps PP (sem empty), s-equi      
<ex>My battery shows as empty.
"""
  [ SYNSEM subj_equi_empty_prep_verb ].

; stop working
; go shopping
v_vp_seq-prp_le := main_verb_mliszt & 
"""
Cmps VP (pres prtcpl), s-equi    
<ex>B loves playing chess.
"""
  [ SYNSEM subj_equi_prp_verb ].

; help
v_vp_seq-bse_le := main_verb_mliszt & 
"""
Cmps VP (base form), s-equi      
<ex>B helped finish the paper.
"""
  [ SYNSEM subj_equi_bse_verb ].

; 'I will go read', 'Go read' '*He went read'
v_vp_seq-go_le := main_verb_mliszt & 
"""
Base form, Cmps VP (base) s-equi 
<ex>B will go play chess.
"""
  [ SYNSEM subj_equi_bse_or_n3sg_verb ].

; 'We needn't stay' but "*He needs not stay", "*We will need not stay"
; cf. "He needn't stay" (irreg)
v_vp_ssr-n3sg_le := main_verb_newltop &
"""
Non-3sg, Cmps VP (base), raising
<ex>We needn't wait here.
"""
  [ SYNSEM subj_rais_bse_n3sg_verb ].

; "need not"
v_vp_ssr-n3sg-r_le := aux_verb_word_super &
"""
Non-3sg, Cmps VP (base) s-raising, oblig adverb
<ex>We need only wait here.
"""
  [ INFLECTD +,
    SYNSEM subj_rais_bse_n3sg_adv_verb ].

; have yet to
v_p-vp_ssr_le := ssr_particle_v_lexent &
"""
Cmps Prtcl, VP (inf), s-raising  
<ex>B has yet to win.
"""
  [ SYNSEM.LKEYS [ --COMPKEY #cmin,
                   --+COMPKEY #cmin ] ].

; they try and find it
v_vp_seq-and_le := main_verb & 
"""
Pres n3sg, Cmps VP (and), s-equi 
<ex>They try and find it.
"""
  [ SYNSEM subj_equi_and_fin_verb ].

; he will try and find it
v_vp_seq-and-bse_le := main_verb & 
"""
Base form, Cmps VP (and), s-equi 
<ex>B will try and find it.
"""
  [ SYNSEM subj_equi_and_bse_verb ].

; couldn't help but notice
v_vp_seq-but_le := main_verb & 
"""
Base form, Cmps VP (but), s-equi 
<ex>B couldn't help but win.
"""
  [ SYNSEM subj_equi_but_verb ].

v_np-vp_oeq_le := main_verb & 
"""
Cmps NP,VP (inf), o-equi         
<ex>B invited C to stay.
"""
  [ SYNSEM obj_equi_verb ].

v_np-vp_oeq-ntr_le := main_verb & 
"""
Cmps NP,VP (inf), o-equi, no passive
<ex>B got C to stay.
"""
  [ SYNSEM obj_equi_non_trans_verb ].

; help
v_np-vp_oeq-bse_le := main_verb_mliszt & 
"""
Cmps NP,VP (base), o-equi        
<ex>B helped C win.
"""
  [ SYNSEM obj_equi_bse_verb ].

v_np-vp_oeq-psv_le := main_verb & 
"""
Cmps NP,VP (inf), o-equi, only passive complement
<ex>The teacher promised the students to be allowed to play outside.
"""
  [ SYNSEM obj_equi_trans_psv_verb ].

v_np-prd_oeq_le := main_verb_mliszt & 
"""
Cmps NP,Pred-phr, o-equi         
<ex>B proved C wrong.
"""
  [ SYNSEM obj_equi_prd_verb ].

v_np-ap_oeq_le := main_verb_mliszt & 
"""
Cmps NP,Adj-phr, o-equi          
<ex>B imagined C taller.
"""
  [ SYNSEM obj_equi_prd_adj_verb ].

; have x ready
; have x in stock
; I want x confirmed
; get x ready
v_np-prd_oeq-ntr_le := main_verb_mliszt & 
"""
Cmps NP,Prd-phr, o-equi, no psv  
<ex>B wanted C ready.
"""
  [ SYNSEM obj_equi_non_trans_prd_verb &
	   [ LKEYS [ --OCOMPKEY #ocmin,
                     --+OCOMPKEY #ocmin ] ] ].


; I have X to get rid of
v_np-vpslnp_oeq_le := main_verb & 
"""
Cmps NP and VP/NP, o-equi        
<ex>B had C to talk to.
"""
  [ SYNSEM obj_equi_vpslash_prd_verb &
	   [ LKEYS [ --OCOMPKEY #ocmin,
                     --+OCOMPKEY #ocmin ] ] ].

; prevent X from Ving
v_np-vp_oeq-from_le := main_verb & 
"""
Cmps NP,VP (prprt, from), o-equi 
<ex>B excused C from playing.
"""
  [ SYNSEM obj_equi_from_verb ].


; promise
v_np-vp_aeq-ntr_le := main_verb & 
"""
Cmps NP,VP (inf), a-equi, no psv 
<ex>B promised C to win.
"""
  [ SYNSEM anom_equi_verb &
	   [ LOCAL.CAT [ VAL.COMPS.FIRST.OPT + ] ] ].

; use X to do Y
v_np-vp_aeq_le := main_verb & 
"""
Cmps NP,VP (inf), a-equi         
<ex>B used C to reach D.
"""
  [ SYNSEM anom_equi_trans_verb ].

; ask X to be allowed to Y
v_np-vp_aeq-psv_le := main_verb & 
"""
Cmps NP,VP (inf), a-equi, passive VP complement only         
<ex>B asked C to be allowed to leave.
"""
  [ SYNSEM anom_equi_psv_verb ].

; kim took too long to arrive
v_np-vp_aeq-noel_le := main_verb & 
"""
Cmps NP,VP (inf), a-equi, no ell 
<ex>B took an hour to win.
"""
  [ SYNSEM anom_equi_oblig_verb ].

; have trouble sleeping
v_np-vp_aeq-prp_le := main_verb_mliszt & 
"""
Cmps NP,VP (prprt), a-equi       
<ex>B had trouble sleeping.
"""
  [ SYNSEM anom_equi_prp_verb ].

; turn out to
v_p-vp_seq_le := main_verb & 
"""
Cmps Prtcl,VP, s-equi            
<ex>B turned out to be wrong.
"""
  [ SYNSEM particle_inf_verb &
	   [ LKEYS [ --COMPKEY #cmin,
                      --+COMPKEY #cmin ] ] ].

; keep on driving
v_p-vp_prp_le := main_verb_mliszt & 
"""
Cmps Prtcl,VP                    
<ex>B kept on winning.
"""
  [ SYNSEM particle_prp_verb &
	   [ LKEYS [ --COMPKEY #cmin,
                      --+COMPKEY #cmin ] ] ].

v_p-cp_le := main_verb & 
"""
Cmps Prtcl,S(fin, +/-Q, opt cmp) 
<ex>B found out (that) C won.
"""
  [ SYNSEM particle_cp_verb &
	   [ LKEYS [ --COMPKEY #cmin,
                      --+COMPKEY #cmin ] ] ].

v_p-pp*-cp_le := main_verb & 
"""
Cmps Prtcl,PP(opt,sel),Scomp     
<ex>B pointed out to C that S.
"""
  [ SYNSEM particle_pp_cp_verb &
	   [ LKEYS [ --COMPKEY #cmin,
                      --+COMPKEY #cmin ] ] ].

v_p-vp_oeq_le := main_verb & 
"""
Cmps Prtcl,VP, o-equi            
<ex>B geared up C to win.
"""
  [ SYNSEM particle_oeq_verb &
	   [ LKEYS [ --COMPKEY #cmin,
                      --+COMPKEY #cmin ] ] ].

v_pp-vp_oeq_le := main_verb & 
"""
Cmps PP(sel),VP, o-equi          
<ex>B appealed to C to win.
"""
  [ SYNSEM oeq_pp_inf_verb &
	   [ LOCAL.CAT [ VAL.COMPS.FIRST.OPT - ],
	     LKEYS [ --COMPKEY #cmin,
                      --+COMPKEY #cmin ] ] ].

v_pp-vp_oeq-bse_le := main_verb & 
"""
Cmps PP(sel),VP, o-equi          
<ex>B listened to C sing.
"""
  [ SYNSEM oeq_pp_bse_verb &
	   [ LOCAL.CAT [ VAL.COMPS.FIRST.OPT - ],
	     LKEYS [ --COMPKEY #cmin,
		     --+COMPKEY #cmin ] ] ].

v_pp*-vp_oeq_le := main_verb & 
"""
Cmps optional PP(sel),VP, o-equi
<ex>B signaled (to C) to open the door.
"""
  [ SYNSEM oeq_pp_inf_verb &
	   [ LKEYS [ --COMPKEY #cmin,
                      --+COMPKEY #cmin ] ] ].

;; leave it to kim to arrange the meeting
v_it-pp-vp_oeq_le := main_verb & 
"""
Cmps Expl-it,PP,VP, o-equi       
<ex>B left it to C to hire D.
"""
  [ SYNSEM expl_pp_inf_oeq_verb &
	   [ LKEYS [ --OCOMPKEY #ocmin,
                     --+OCOMPKEY #ocmin ] ] ].

;; arrange (with kim) to meet her
v_pp-vp_seq_le := main_verb & 
"""
Cmps PP,VP, s-equi               
<ex>B arranged with C to stay.
"""
  [ SYNSEM pp_inf_seq_verb &
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT + ], [ OPT - ] > ] ] ].

;; arrange it with kim to meet her
v_it-pp-vp_seq_le := main_verb & 
"""
Cmps Expl-it,PP,VP s-equi        
<ex>B arranged it with C to X.
"""
  [ SYNSEM expl_pp_inf_seq_verb &
	   [ LKEYS [ --OCOMPKEY #ocmin,
                     --+OCOMPKEY #ocmin ] ] ].

;; *it bothers kim that sally left*; and by lex rule: *it bothers kim to go*
v_np-cp_it-s_le := main_verb & 
"""
It-subj, Cmps NP,S(fin/inf)      
<ex>It bothered B that C won.
"""
  [ SYNSEM expl_it_subj_np_cp_verb ].

; ticks NP off that S
v_np-p-cp_it-s_le := main_verb & 
"""
It-subj, Cmps NP,ptcl,S
<ex>It ticked him off that we won.
"""
  [ SYNSEM expl_it_subj_np_ptcl_cp_verb ].

;; That Kim lost suggests that Sandy won. 
;; It suggests that Sandy won that Kim lost.
v_cp-cp_it-s_le := main_verb & 
"""
It-subj, S ,S 
<ex>That B lost suggests that C won.
"""
  [ SYNSEM expl_it_subj_cp_cp_verb ].

; cost
v_np-np-cp_it-s_le := main_verb & 
"""
It-subj, Cmps NP,NP,S(fin/inf)   
<ex>It cost B $5 for C to go.
"""
  [ SYNSEM expl_it_subj_np_np_cp_inf_verb ].

; make (?)
v_np-np-cp_it-ob_le := main_verb & 
"""
It-subj, Cmps NP,NP,S(fin/inf)   
<ex>It makes Kim the winner that the horse died.
"""
  [ SYNSEM expl_it_subj_np_np_cp_verb ].

; matter
v_np-pp*-cp_it-s_le := main_verb & 
"""
It-subj, Cmps NP,PP,S(fin)   
<ex>It matters a lot to Kim that S.
"""
  [ SYNSEM expl_it_subj_np_pp_cp_verb ].

; take: 'took him ten minutes to leave'
v_np*-np-vp_it-s_le := main_verb & 
"""
It-subj, Cmps NP,NP,VP           
<ex>It took B an hour to go.
"""
  [ SYNSEM expl_it_subj_np_np_vp_inf_verb &
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT + ], [ OPT - ], [ ] > ] ] ].

; take: 'took (him) until Friday to leave'
v_np*-pp-cp_it-s-inf_le := main_verb & 
"""
It-subj, Cmps NP,PP,inf-VP-or-CP           
<ex>It took B until Friday to leave.
"""
  [ SYNSEM expl_it_subj_np_pp_cp_inf_verb &
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT + ], [ OPT - ], [ ] > ] ] ].

; 'it advances our cause (a lot) to take this step"
; "it will cost us (a lot) to win"
v_np-np*-vp_it-s_le := main_verb & 
"""
It-subj, Cmps NP,(NP),VP         
<ex>It cost B ($5) to go.
"""
  [ SYNSEM expl_it_subj_np_np_vp_inf_verb &
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT - ], unexpressed, [ ] > ] ] ].

; call
v_it-pp-cp_le := main_verb & 
"""
Cmps it,PP,S(fin)   
<ex>He called it to their attention that S
"""
  [ SYNSEM np_expl_pp_cp_verb ].

; cost/take
v_np-np-vpslnp_tgh_le := main_verb & 
"""
subj,Cmps NP,NP,VP/NP
<ex>The book took (B) an hour to read.
"""
  [ SYNSEM obj_equi_vpslash_four_arg_verb ].

; make (only)
v_np-prd-cp_it-s_le := main_verb & 
"""
It-subj, Cmps NP,PrdP ,S(fin/inf)   
<ex>It made B happy for C to go.
"""
  [ SYNSEM expl_it_subj_np_prdp_cp_verb ].

;; it seems (to kim) that sandy left
v_pp*-cp_it-s_le := main_verb & 
"""
It-subj, Cmps (PP),S(fin)        
<ex>It seems to B that K won.
"""
  [ SYNSEM expl_it_subj_pp*_cp_verb ].

;; it occurred *(to me) that Sandy left
v_pp-cp_it-s_le := main_verb & 
"""
It-subj, Cmps PP,S(fin)          
<ex>It occurred to B to look.
"""
  [ SYNSEM expl_it_subj_pp_cp_verb ].

;; it looks (to kim) like Sandy left
v_pp*-cp_like_le := main_verb & 
"""
It-subj, Cmps (PP),S(like)       
<ex>It felt (to B) like K won.
"""
  [ SYNSEM expl_it_subj_pp_cp_like_verb ].

;; It doesn't matter who stays
v_pp*-cp_it-unsp_le := main_verb & 
"""
It-subj, Cmps (PP),S(+/-Q)       
<ex>It mattered whether S won.
"""
  [ SYNSEM expl_it_subj_pp_cp_unsp_verb ].

;; It dawned on Kim that ...
v_pp-cp_it-s-e_le := main_verb & 
"""
It-subj, Cmps PP,S(fin, no to)   
<ex>It dawned on B that K won.
"""
  [ SYNSEM expl_it_subj_ppnoto_cp_verb ].

; it turns out that kim arrived
v_p-cp_it-s_le := main_verb & 
"""
It-subj, Cmps Prtcl,S            
<ex>It turned out that S won.
"""
  [ SYNSEM expl_it_subj_prtcl_cp_verb ].

; It follows that ...
v_cp_it-s_le := main_verb & 
"""
It-subj, Cmps S                  
<ex>It followed that S won.
"""
  [ SYNSEM expl_it_subj_cp_verb ].

; it would help/wouldn't hurt to know ...
v_vp_it-s_le := main_verb & 
"""
It-subj, Cmps VP                 
<ex>It doesn't pay to grumble.
"""
  [ SYNSEM expl_it_subj_vp_verb ].

; bet
v_np-np-cp_le := main_verb & 
"""
Cmps NP,NP,S                     
<ex>B bet C $5 that S won.
"""
  [ SYNSEM np_np_cp_verb ].

; say
v_pp*-cp_le := norm_basic_main_verb & 
"""
Cmps PP(sel, opt),S non-Q
<ex>B said to C that D won.
"""
  [ INFLECTD -,
    SYNSEM pp_cp_fin_prop_verb ].

; say
v_pp*-cp_fin-imp_le := norm_basic_main_verb & 
"""
Cmps PP(sel, opt),S(fin/inf), allow imperatives
<ex>B said to C that D won.
"""
  [ INFLECTD -,
    SYNSEM pp_cp_fin_unspec_verb ].


; predict
v_pp*-cp_unsp_le := main_verb & 
"""
Cmps PP(sel, opt),S(fin/inf, +/-Q),
<ex>B predicted whether C would win.
"""
  [ SYNSEM pp_cp_fin_pq_verb ].

v_pp*-cp_fin_le := main_verb & 
"""
Cmps PP(sel, opt),S(fin)         
<ex>B joked to C that D won.
"""
  [ SYNSEM pp*_cp_fin_verb ].

v_pp-cp_fin_le := main_verb & 
"""
Cmps PP(sel, oblig),S(fin)         
<ex>B impressed on C that D won.
"""
  [ SYNSEM pp_cp_fin_verb,
    ALTS.INVQ - ].

v_pp*-cp_inf_le := main_verb & 
"""
Cmps PP(sel, oblig),S(inf)         
<ex>B pleaded (with C) for D to be hired.
"""
  [ SYNSEM pp*_cp_inf_verb ].

; 'see to it that X'
v_pp-it-cp_le := main_verb & 
"""
Cmps PP(sel,expl-it),S(fin)      
<ex>B saw to it that C won.
"""
  [ SYNSEM pp_expl_cp_verb ].

; would like
v_np-vp_sor-ntr_le := main_verb_mliszt & 
"""
Cmps NP,VP, s-o-raising, no psv  
<ex>B wanted it to rain.
"""
  [ SYNSEM sor_non_trans_verb ].

v_np-vp_sor_le := main_verb_mliszt & 
"""
Cmps NP,VP, s-o-raising          
<ex>B expected it to rain.
"""
  [ SYNSEM sor_verb ].

v_np-vp_bse_le := main_verb_mliszt & 
"""
Cmps NP,VP(bse), s-o-raising     
<ex>B heard it rain.
"""
  [ SYNSEM sorb_verb ].

; make: 'kim made it clear that sandy arrived'
v_np-prd_sor_le := main_verb_mliszt & 
"""
Cmps NP,Adj, s-o-raising         
<ex>The smog made it cloudy.
"""
  [ SYNSEM sor_prd_verb ].

v_np-np_le := main_verb & 
"""
Cmps NP(obl),NP(obl), dative alt 
<ex>B gave C the book.
"""
  [ SYNSEM ditrans_verb &
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT - ], [ OPT - ] > ],
	     LKEYS [ --OCOMPKEY #ocmin,
		     --+OCOMPKEY #ocmin ] ] ].

; 'leave (bequeath)'
v_np-np*_le := main_verb & 
"""
Cmps NP(obl),NP(opt), dative alt 
<ex>B left the book to C.
"""
  [ SYNSEM ditrans_verb & 
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT - ], [ OPT + ] > ] ] ].

v_np*-np_le := main_verb & 
"""
Cmps NP(opt),NP(oblig), dative alt 
<ex>B paid C the money.
"""
  [ SYNSEM ditrans_verb & 
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT + ], [ OPT + ] > ] ] ].

v_np-np_only_le := main_verb & 
"""
Cmps NP(obl),NP(obl), no dat alt 
<ex>B declared C the winner.
"""
  [ ALTS.DATIVE -,
    SYNSEM ditrans_only_verb & 
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT - ], [ OPT - ] > ],
             LKEYS [ --OCOMPKEY #ocmin,
                     --+OCOMPKEY #ocmin ] ] ].

v_np*-np*_only_le := main_verb & 
"""
Cmps NP(opt),NP(opt), no dat alt 
<ex>B asked C the question.
"""
  [ ALTS.DATIVE -,
    SYNSEM ditrans_only_verb & 
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT + ], [ OPT + ] > ] ] ].

v_np-np_idm_le := main_verb & 
"""
Cmps NP(obl),NP(obl), idiom      
<ex>B called it quits.
"""
  [ ALTS.DATIVE -,
    SYNSEM ditrans_only_verb & 
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT - ], [ OPT - ] > ],
             LKEYS [ --OCOMPKEY #ocmin,
                     --+OCOMPKEY #ocmin ] ],
    IDIOM + ].

;; talk
v_pp*-pp*_le := main_verb & 
"""
Cmps PP(opt),PP(opt)             
<ex>B lied to C about D.
"""
  [ SYNSEM double_pp_verb &
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT + ], [ OPT + ] > ] ] ].

v_pp-pp_le := main_verb & 
"""
Cmps PP(obl),PP(obl)             
<ex>B ranged from C to D.
"""
  [ SYNSEM double_pp_verb & 
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT - ], [ OPT - ] > ],
             LKEYS [ --COMPKEY #cmin,
                     --+COMPKEY #cmin,
                     --OCOMPKEY #ocmin,
                     --+OCOMPKEY #ocmin ] ] ].

v_pp-pp_e_le := main_verb & 
"""
Cmps PP(obl),PP(sel,obl)         
<ex>B reported on C to D.
"""
  [ SYNSEM double_pp_empty_pp_verb &
	   [ LKEYS [ --COMPKEY #cmin,
                     --+COMPKEY #cmin,
                     --OCOMPKEY #ocmin,
                     --+OCOMPKEY #ocmin ] ] ].

v_np*-pp*-ppto_le := main_verb & 
"""
Cmps NP(opt),PP(sel,opt),PP(to)  
<ex>B changed C from D to E.
"""
  [ SYNSEM np_trans_double_ppto_verb & 
	   [ LOCAL.CAT [ VAL.COMPS.FIRST.OPT + ] ] ].

v_np-pp*-ppto_le := main_verb & 
"""
Cmps NP(obl),PP(sel,opt),PP(to)  
<ex>B converted C from D to E.
"""
  [ SYNSEM np_trans_double_ppto_verb &
	   [ LOCAL.CAT [ VAL.COMPS.FIRST.OPT - ] ] ].

v_np-pp*-ppfor_le := main_verb & 
"""
Cmps NP(oblig),PP(sel,opt),PP(for) 
<ex>B paid money to C for D.
"""
  [ SYNSEM np_trans_double_ppfor_verb ].

v_pp_e_le := main_verb & 
"""
Cmps PP(sel,obl)                 
<ex>B looked after C.
"""
  [ SYNSEM empty_prep_intrans_verb &
	   [ LKEYS [ --COMPKEY #cmin,
                     --+COMPKEY #cmin ] ] ].

v_pp_e3_le := main_verb & 
"""
Cmps PP(sel,obl)                 
<ex>B gave to worthy causes
"""
  [ SYNSEM empty_prep_intrans_arg3_verb & 
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT - ] > ],
             LKEYS [ --COMPKEY #cmin,
                     --+COMPKEY #cmin ] ] ].

v_pp_refl-idm_le := main_verb & 
"""
PP complement, reflexive idiom
<ex>B came out of her shell.
"""
  [ SYNSEM empty_prep_intrans_idiom_verb &
	   [ LKEYS [ --COMPKEY #cmin,
                     --+COMPKEY #cmin ] ] ].


v_np-pp_e_le := main_verb & 
"""
Cmps NP(obl),PP(sel.obl)         
<ex>B compared C with D.
"""
  [ SYNSEM empty_prep_trans_verb &
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT - ], [ OPT - ] > ],
             LKEYS [ --OCOMPKEY #ocmin,
                     --+OCOMPKEY #ocmin ] ] ].

v_np*-pp_e_le := main_verb & 
"""
Cmps NP(opt),PP(sel,obl)         
<ex>B separated C from D.
"""
  [ SYNSEM empty_prep_trans_verb & 
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT + ], [ OPT - ] > ],
             LKEYS [ --OCOMPKEY #ocmin,
                     --+OCOMPKEY #ocmin ] ] ].

v_np-pp*_e-imp_le := main_verb & 
"""
Cmps NP(obl),PP(sel,opt), hd_imp_c  
<ex>Thank you for D.
"""
  [ SYNSEM empty_prep_trans_verb & 
	   [ LOCAL [ CAT [ HEAD [ VFORM fin ],
                           VAL [ SUBJ < [ LOCAL.CONT.HOOK.INDEX #ind ] >,
                                 COMPS < [ OPT - ], [ OPT + ] > ] ],
                     AGR #ind ],
             LKEYS [ KEYREL.ARG1 #ind & [ PNG.PN 1 ] ] ] ].

; take Kim for granted
v_np-pp_e-idm_le := main_verb & 
"""
NP and empty-PP complements, idiom
Cmps NP(obl),PP(sel,obl), idiom  
<ex>B took C for granted.
"""
  [ SYNSEM empty_prep_trans_verb & 
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT -, 
				       LOCAL.CAT.HEAD basic_noun ],
				     [ OPT - ] > ],
             LKEYS [ --OCOMPKEY #ocmin,
                     --+OCOMPKEY #ocmin ] ],
    IDIOM + ].

v_np-pp_e-refl-idm_le := main_verb & 
"""
Cmps reflexive NP(obl),PP(sel,obl), idiom  
<ex>B cast her lot with C.
"""
  [ SYNSEM empty_prep_trans_refl_idiom_verb &
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT - ], [ OPT - ] > ],
             LKEYS [ --OCOMPKEY #ocmin,
                     --+OCOMPKEY #ocmin ] ] ].

v_np-pp_e-refl2-idm_le := main_verb & 
"""
Cmps reflexive NP(obl),reflexive PP(sel,obl), idiom  
<ex>B wore his heart on his sleeve.
"""
  [ SYNSEM empty_prep_trans_refl_refl_idm_verb &
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT - ], [ OPT - ] > ],
             LKEYS [ --OCOMPKEY #ocmin,
                     --+OCOMPKEY #ocmin ] ] ].

; came into being
v_pp_e-idm_le := main_verb & 
"""
Cmps NP(obl), idiom              
<ex>B came into being.
"""
  [ SYNSEM empty_prep_intrans_verb & 
	   [ LKEYS [ --COMPKEY #cmin,
                     --+COMPKEY #cmin ] ],
    IDIOM + ].

; 'interest kim in'
v_np-pp_e-ntr_le := main_verb & 
"""
Cmps NP(obl),PP(sel,obl), no psv 
<ex>B interested C in D.
"""
  [ SYNSEM empty_prep_non_trans_verb ].

;; 'prefer it if ...'
v_it-cp_le := main_verb &
"""
Cmps Expl-it,S(fin,+/-Q)         
<ex>B prefers it if C wins.
"""
  [ SYNSEM expl_obj_cp_verb ].

;; 'hate it that ...'
v_it-cp_prop-c_le := main_verb &
"""
Cmps Expl-it,S(fin,-Q),obl cplzr 
<ex>B hates it that C won.
"""
  [ SYNSEM expl_obj_cpcmp_verb ].

;; 'take it (that) ...'
v_it-cp_prop_le := main_verb &
"""
Cmps Expl-it,S(fin,-Q)           
<ex>B took it (that) C won.
"""
  [ SYNSEM expl_obj_cpprop_verb ].

; 'make it clear that ...'
v_it-prd_le := main_verb_mliszt &
"""
Cmps Expl-it,Pred-phr            
<ex>B made it clear that C won
"""
  [ SYNSEM expl_obj_prd_verb ].

v_np_le := main_verb & 
"""
Cmps NP(obl)                     
<ex>B chased C.
"""
  [ SYNSEM np_trans_verb &
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT - ] > ] ] ].

v_np*_le := main_verb & 
"""
Cmps NP(opt)                     
<ex>B cleaned the room.
"""
  [ SYNSEM np_trans_verb ].

;; DPF 2017-04-27 - No longer see why we would restrict these verbs (often
;; alternating with inchoatives) to exclude gerund complements, since we
;; need them for e.g. *they increased spending*.  So changed [HEAD basic_noun]
;; to supnoun on complement.
;; DPF 2020-05-14 - Perhaps a confusion about the name: noger here means the
;; verb itself does not undergo the (nominal) gerund lexical rule.  But we do
;; need a lexical verb type that excludes gerund complements, as for *seem*
;; in *He seemed arriving*, and does not passivize.
;;
v_np_noger_le := main_verb & 
"""
Cmps NP(obl,nongerund)           
<ex>B continued the tour.
"""
  [ ALTS [ NGER -,
	   VADJ - ],
    SYNSEM np_trans_verb &
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT -,
				       LOCAL.CAT.HEAD supnoun ] > ] ] ].

v_np*_noger_le := main_verb & 
"""
Cmps NP(opt,nongerund)           
<ex>B blended until the milkshake was smooth.
"""
  [ ALTS [ VADJ -,
           NGER - ],
    SYNSEM np_trans_verb &
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT +,
				       LOCAL.CAT.HEAD supnoun ] > ] ] ].

v_np_do-be_le := basic_main_verb_sans_key &
"""
Cmps NP(gap), only for do (but not doing) in do-be construction
<ex>The first thing B did was hire C.
"""
  [ SYNSEM do_be_verb &
	   [ LOCAL.CAT.HEAD [ VFORM non_prespart,
			      INV - ],
	     LKEYS.KEYREL.ARG2.IFORM non_prespart ],
    INFLECTD - ].


v_np_do-ing-be_le := basic_main_verb_sans_key &
"""
Cmps NP(gap), only for doing in do-be construction
<ex>The thing B was doing was hiring C.
"""
  [ INFLECTD -,
    SYNSEM do_be_verb &
	   [ LOCAL.CAT.HEAD [ VFORM prp,
			      INV - ],
	     LKEYS.KEYREL.ARG2.IFORM prp ] ].

v_np_done-be_le := basic_main_verb_sans_key &
"""
Cmps NP(gap), only for do (but not doing) in do-be construction
<ex>The first thing that must be done is hire C.
"""
  [ SYNSEM do_be_verb_passive &
	   [ LOCAL.CAT.HEAD.INV -,
	     LKEYS.KEYREL.ARG2.IFORM non_prespart ],
    INFLECTD + ].

v_np*_nfin_le := main_verb & 
"""
Cmps NP(opt), nonfin             
<ex>B should beware C.
"""
  [ SYNSEM np_trans_nfin_verb ].

v_np_n3s-bse_le := main_verb & 
"""
Cmps NP(opt), non-3sg or base
<ex>They underly C.
"""
  [ SYNSEM np_non_trans_verb &
           [ LOCAL bse_or_non3sg_verb &
		   [ CAT.VAL.COMPS < [ OPT - ] > ] ] ].

v_np_refl-idm_le := main_verb_mliszt & 
"""
Cmps reflexive NP, idiom                   
<ex>We racked our brains.
"""
  [ SYNSEM np_trans_refl_verb,
    IDIOM + ].

v_p-np_refl-idm_le := main_verb_mliszt & 
"""
Cmps particle + reflexive NP, idiom                   
<ex>We made up our minds.
"""
  [ SYNSEM np_particle_idiom_verb &
	   [ LKEYS [ --COMPKEY #cmin,
                     --+COMPKEY #cmin ] ],
    IDIOM + ].

v_np_sg-idm_le := v_np_idm_lexent &
"""
Cmps non-reflexive NP, singular, idiom                   
<ex>He kicked the bucket.
"""
  [ SYNSEM.LOCAL.CAT.VAL.COMPS < [ LOCAL.AGR.PNG.PN 3s ] > ].

v_np_pl-idm_le := v_np_idm_lexent &
"""
Cmps non-reflexive NP, plural, idiom                   
<ex>He sucked eggs.
"""
  [ SYNSEM.LOCAL.CAT.VAL.COMPS < [ LOCAL.AGR.PNG.PN 3p ] > ].

; 'elected him president'
v_np-nb_le := main_verb_mliszt & 
"""
Cmps NP(obl),Nbar(obl)           
<ex>We elected B president.
"""
  [ SYNSEM ditrans_np_nbar_verb ].

; 'see picture, page 2'
v_nb-np*_imp_le := main_verb_mliszt & 
"""
Cmps Nbar(obl), NP(opt), hd_imp_c   
<ex>See pamphlet, page 2.
"""
  [ SYNSEM ditrans_nbar_np_verb ].

v_pp_e-nfin_le := main_verb & 
"""
Cmps NP(obl), nonfin             
<ex>B should beware of C.
"""
  [ SYNSEM empty_prep_intrans_bse_verb ].

v_np-pp_le := main_verb_mliszt & 
"""
Cmps NP(obl),PP(obl)             
<ex>B named C after D.
"""
  [ SYNSEM prep_trans_verb & 
	   [ LKEYS [ --OCOMPKEY #ocmin,
                     --+OCOMPKEY #ocmin ] ] ].

;; takes dir_state_modable_rel oblique complement.
;; put (takes variety of PP complements, so avoid chart dependency filter)
v_np-pp_prop_le := main_verb_mliszt & 
"""
Cmps NP(obl),PP(obl), state rslt 
<ex>B put C on D.
"""
  [ SYNSEM prep_trans_dir_verb ].

;; change one's mind about
v_np-pp_nmod_le := main_verb_mliszt &
"""
Cmps NP(obl),PP(obl, nmodifier)  
<ex>We change our mind about C
"""
  [ SYNSEM prep_trans_verb_nmod &
	   [ LKEYS [ --OCOMPKEY #ocmin,
                     --+OCOMPKEY #ocmin ] ] ].

v_np-pp_dir_le := main_verb_mliszt & 
"""
Cmps NP(obl),PP(dir, obl)
<ex>B raced C into D.
"""
  [ SYNSEM prep_trans_dir_noprop_verb ].

v_np-pp_oeq-as_le := main_verb &
"""
Cmps NP(obl),PP-prd(obl,as), o-equi  
<ex>B classifies C as important.
"""
  [ SYNSEM np_as_verb &
	   [ LKEYS [ --OCOMPKEY #ocmin,
                     --+OCOMPKEY #ocmin ] ] ].

v_np_ntr_le := main_verb & 
"""
Cmps NP(obl), no psv             
<ex>B remains C.
"""
  [ SYNSEM np_non_trans_verb &
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT - ] > ] ] ].

v_np*_ntr_le := main_verb & 
"""
Cmps NP(opt), no psv             
<ex>B promised C.
"""
  [ SYNSEM np_non_trans_verb & 
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT + ] > ] ] ].

v_np_ntr-noger_le := main_verb & 
"""
Cmps NP(obl), no psv, no gerund complement             
<ex>B seemed the best choice.
<nex>B seemed arriving.
"""
  [ SYNSEM np_non_trans_verb &
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT -,
				       LOCAL.CAT.HEAD basic_noun] > ] ] ].

; 'become'
v_np_id_le := main_verb & 
"""
Cmps NP, become                  
<ex>B became C.
"""
  [ SYNSEM np_non_trans_verb & 
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT - ] > ] ] ].

; 'become president'
v_nb_le := main_verb_mliszt & 
"""
Cmps Nbar(obl,pred nom)          
<ex>B became president.
"""
  [ SYNSEM nbar_verb ].

; 'become obvious that Kim arrived.'
v_prd_ssr-va_le := main_verb_mliszt & 
"""
Cmps VP or AP, subj-raising, expl-it subj
<ex>It became obvious that Kim arrived.
"""
  [ SYNSEM ssr_prdp_verb ].

; 'stood guard'
v_nb_idm_le := main_verb & 
"""
Cmps Nbar(obl), idiom            
<ex>B stood guard.
"""
  [ SYNSEM np_non_trans_nb_verb,
    IDIOM + ].

v_np-pp_seq-as_le := main_verb_mliszt & 
"""
Cmps NP(obl),PP(obl,as), s-equi  
<ex>B struck C as without D.
"""
  [ SYNSEM np_as_seq_verb &
	   [ LKEYS [ --OCOMPKEY #ocmin,
                     --+OCOMPKEY #ocmin ] ] ].

v_p_le := main_verb & 
"""
Cmps Prtcl                       
<ex>B turned up.
"""
  [ SYNSEM particle_verb &
	   [ LKEYS [ --COMPKEY #cmin,
                     --+COMPKEY #cmin ] ] ].

v_p_3only_le := main_verb & 
"""
Cmps Prtcl, subj 3per            
<ex>Something is going on.
"""
  [ SYNSEM particle_3only_verb &
	   [ LKEYS [ --COMPKEY #cmin,
                     --+COMPKEY #cmin ] ] ].
v_p-np_le := main_verb & 
"""
Cmps Prtcl,NP(obl), order alt    
<ex>B picked up the book.
"""
  [ SYNSEM np_particle_verb &
	   [ LKEYS [ --COMPKEY #cmin,
                     --+COMPKEY #cmin ] ] ].
v_np-p_le := main_verb & 
"""
Cmps NP(obl),Prtcl, no order alt 
<ex>We will see you around.
"""
  [ SYNSEM np_particle_verb &
	   [ LKEYS [ --COMPKEY #ocmin,
                     --+COMPKEY #ocmin ] ],
    ALTS.NPPART - ].

v_np-p_idm_le := main_verb & 
"""
Cmps NP(obl),Prtcl, no order alt 
<ex>We will see you around.
"""
  [ SYNSEM np_particle_idiom_verb &
	   [ LKEYS [ --COMPKEY #ocmin,
                     --+COMPKEY #ocmin ] ],
    ALTS.NPPART -,
    IDIOM + ].

v_p-prd_le := main_verb & 
"""
Cmps Prtcl,Pred-phr              
<ex>B ended up sick.
"""
  [ SYNSEM particle_prd_verb &
	   [ LKEYS [ --COMPKEY #cmin,
                     --+COMPKEY #cmin ] ] ].
v_p-pp_le := main_verb & 
"""
Cmps Prtcl,PP(obl)               
<ex>B caught up on C.
"""
  [ SYNSEM particle_pp_verb ].

v_p-pp_e_le := main_verb & 
"""
Cmps Prtcl,PP(obl,sel)           
<ex>B ran out of C.
"""
  [ SYNSEM particle_empty_pp_verb &
	   [ LOCAL.CAT [ VAL.COMPS < synsem, [ OPT - ] > ],
             LKEYS [ --COMPKEY #cmin,
                     --+COMPKEY #cmin,
                     --OCOMPKEY #ocmin,
                     --+OCOMPKEY #ocmin ] ] ].

v_p-pp*_e_le := main_verb & 
"""
Cmps Prtcl,PP(opt,sel)           
<ex>B heard back from C.
"""
  [ SYNSEM particle_empty_pp_verb &
	   [ LOCAL.CAT [ VAL.COMPS < synsem, [ OPT + ] > ],
             LKEYS [ --COMPKEY #cmin,
                     --+COMPKEY #cmin ] ] ].

; 'boils it down to'
v_p-np-pp_to_le := main_verb & 
"""
Cmps Prtcl,NP(obl),PP(obl,to)    
<ex>B made out the check to C.
"""
  [ SYNSEM np_particle_ppto_verb ].

; 'made up of'
v_p-np-pp_of_le := main_verb & 
"""
Cmps Prtcl,NP(obl),PP(obl,of)    
<ex>This is made up of bricks.
"""
  [ SYNSEM np_particle_ppof_verb ].

v_p-np-pp_for_le := main_verb & 
"""
Cmps Prtcl,NP(obl),PP(obl,for)   
<ex>B traded in B for C.
"""
  [ SYNSEM np_particle_ppfor_verb ].

v_p-np-pp_from_le := main_verb & 
"""
Cmps Prtcl,NP(obl),PP(obl,from)  
<ex>B separated out C from D.
"""
  [ SYNSEM np_particle_ppfrom_verb ].

v_p-np-pp_in_le := main_verb & 
"""
Cmps Prtcl,NP(obl),PP(obl,in)  
<ex>B was caught up in the debate.
"""
  [ SYNSEM np_particle_ppin_verb ].

v_p-np-pp_on_le := main_verb & 
"""
Cmps Prtcl,NP(obl),PP(obl,on)  
<ex>B fobbed this off on C.
"""
  [ SYNSEM np_particle_ppon_verb ].

v_p-np-pp_as_le := main_verb & 
"""
Cmps Prtcl,NP(obl),PP(obl,as)  
<ex>B singled him out as C.
"""
  [ SYNSEM np_particle_ppas_verb ].

v_np-p-np_le := main_verb & 
"""
Cmps NP,Prtcl,NP                 
<ex>B gave C back the book.
"""
  [ SYNSEM np_particle_np_verb &
	   [ LKEYS [ --COMPKEY #cmin,
                     --+COMPKEY #cmin ] ] ].

v_np-p-cp_idm_le := main_verb & 
"""
Cmps NP,Prtcl,CP or VP, fin or inf, maybe only for idioms
<ex>B made up her mind that S.
<ex>B made up her mind to leave.
"""
  [ SYNSEM np_particle_cp_verb &
	   [ LKEYS [ --COMPKEY #cmin,
                     --+COMPKEY #cmin ] ],
    IDIOM + ].

v_np-p-cp_le := main_verb & 
"""
Cmps NP,Prtcl,CP or VP, fin or inf
<ex>B made up her mind that S.
<ex>B made up her mind to leave.
"""
  [ SYNSEM np_particle_cp_verb &
	   [ LOCAL.CAT.VAL.KCMP.LOCAL.CAT s_cat_unspec,
	     LKEYS [ --COMPKEY #cmin,
                     --+COMPKEY #cmin ] ] ].

v_np-p-vp_le := main_verb & 
"""
Cmps NP,Prtcl,VP inf
<ex>B made C out to be crazy.
"""
  [ SYNSEM np_particle_vp_verb &
	   [ LOCAL.CAT.VAL.KCMP.LOCAL.CAT [ HEAD.VFORM inf,
					    VAL.SUBJ < synsem > ],
	     LKEYS [ --COMPKEY #cmin,
                     --+COMPKEY #cmin ] ] ].

v_pp_arg_le := main_verb  & 
"""
Cmps PP, argmt coercion          
<ex>B suggested on Monday.
"""
  [ SYNSEM prep_intrans_event_verb ].

v_pp_gap_le := main_verb  & 
"""
Cmps PP, obligatorily extracted
<ex>They sleep whenever they want.
"""
  [ SYNSEM prep_intrans_gap_verb ].

v_pp_le := main_verb  & 
"""
Cmps PP(obl)                     
<ex>The cat felt like a dog.
"""
  [ SYNSEM prep_intrans_verb &
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT - ] > ],
             LKEYS [ --COMPKEY #cmin,
                     --+COMPKEY #cmin ] ] ].

v_pp*_dir_le := main_verb  & 
"""
Cmps PP(opt,dir)                 
<ex>B crept into the room.
"""
  [ SYNSEM prep_intrans_dir_verb &
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT + ] > ] ] ].

v_pp_dir_le := main_verb  & 
"""
Cmps PP(obl,dir)                 
<ex>B slipped into the room.
"""
  [ SYNSEM prep_intrans_dir_verb & 
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT - ] > ] ] ].

v_pp*_le := main_verb & 
"""
Cmps PP(opt)                     
<ex>B starred in C
"""
  [ SYNSEM prep_intrans_verb & 
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT + ] > ] ] ].

v_pp_unacc_le := main_verb  & 
"""
Cmps PP(opt), unacc              
<ex>The seed grew into a tree.
"""
  [ SYNSEM prep_intrans_unacc_verb ].

v_vp_ssr_le := main_verb & 
"""
Cmps VP(inf), s-s-rais           
<ex>There failed to be a link.
"""
  [ SYNSEM ssr_verb ].

v_vp_ssr-prp_le := main_verb & 
"""
Cmps VP(prp), s-s-rais           
<ex>It finished raining.
"""
  [ SYNSEM ssr_prp_verb ].

; 'have to': no imperative "*Have to leave!"
v_vp_ssr-nimp_le := main_verb & 
"""
Cmps VP(inf), s-s-rais, no hd_imp_c 
<ex>There tend to be problems.
"""
  [ SYNSEM ssr_noimp_verb ].

v_pp-vp_ssr_le := main_verb_mliszt & 
"""
Cmps PP(opt),VP(inf), s-s-rais   
<ex>It seems to B to be windy.
"""
  [ SYNSEM ssr_pp_inf_verb ].

; DPF 29-aug-07 - These verbs like "become" supposedly don't combine with
; predicative VPs, but stative passives often seem to be fine, 
; as in ; "This picture became damaged/associated with Picasso" or
; "We became inspired by the music."  Rather than adding a lexical rule to
; convert them to predicative adjs, consider a HEAD supertype of basic_adj
; and passive-verb.  FIX.

v_ap-pp_seq_le := main_verb_mliszt & 
"""
Cmps AP,PP(opt), s-equi          
<ex>B sounded good to C.
"""
  [ SYNSEM seq_ap_pp_verb ].

v_pp-pp_seq_le := main_verb_mliszt & 
"""
Cmps PP(like),PP(opt), s-equi    
<ex>B sounded like C to D.
"""
  [ SYNSEM seq_pp_pp_verb ].

v_prd-pp_seq_le := main_verb_mliszt & 
"""
Cmps Pred-phr,PP(opt), s-equi    
<ex>B seemed good to C.
"""
  [ SYNSEM seq_aorp_pp_verb,
    ALTS.VADJ - ].

; feel good about
v_ap-pp_about_le := main_verb_mliszt & 
"""
Cmps AP,PP(about, sel), s-equi   
<ex>B felt good about C.
"""
  [ SYNSEM seq_prdp_pp_about_verb ].

v_np-pp_to_le := main_verb & 
"""
Cmps NP(obl),PP(obl,to,sel)      
<ex>B attributed C to D.
"""
  [ SYNSEM empty_to_trans_verb &
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT - ], [ OPT - ] > ],
             LKEYS [ --COMPKEY #cmin,
                     --+COMPKEY #cmin,
                     --OCOMPKEY #ocmin,
                     --+OCOMPKEY #ocmin ] ] ].

v_np-pp_to-unsp_le := main_verb & 
"""
Cmps NP(obl),PP(obl,to,sel), either Nbar or NP complement of `to'
<ex>B promoted C to president/the presidency.
"""
  [ SYNSEM empty_prep_trans_verb &
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT - ], [ OPT - ] > ],
             LKEYS [ --COMPKEY #cmin,
                     --+COMPKEY #cmin,
                     --OCOMPKEY _to_p_sel_rel & #ocmin,
                     --+OCOMPKEY #ocmin ] ] ].

;; DPF 2018-12-20 - This type prevented N-V compounds as in "snow-bound" since
;; the optional selected-for PP blocks these.  So discard this type, and unfold
;; its instances into two types v_np-pp_to_le and v_np_le.
;;

v_np-pp*_to_le := main_verb & 
"""
Cmps NP(obl),PP(opt,to,sel)      
<ex>B converted C to D.
"""
  [ SYNSEM empty_to_trans_verb &
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT - ], [ OPT + ] > ],
             LKEYS [ --COMPKEY #cmin,
                     --+COMPKEY #cmin ] ] ].


v_np*-pp_to_le := main_verb & 
"""
Cmps NP(opt),PP(obl,to,sel)      
<ex>B added C to D.
"""
  [ SYNSEM empty_to_trans_verb &
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT + ], [ OPT - ] > ],
             LKEYS [ --OCOMPKEY #ocmin,
                     --+OCOMPKEY #ocmin ] ] ].

v_np*-pp*_to_le := main_verb & 
"""
Cmps NP(opt),PP(opt,to,sel)      
<ex>B explained C to D.
"""
  [ SYNSEM empty_to_trans_verb &
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT + ], [ OPT + ] > ] ] ].

v_-_unacc_le := main_verb & 
"""
No cmps, unaccusative            
<ex>The plate gleamed.
"""
  [ SYNSEM unacc_verb ].

v_-_le := main_verb & 
"""
No cmps, unergative              
<ex>B arose.
"""
  [ SYNSEM unerg_verb ].

v_-_imp_le := main_verb & 
"""
No cmps, unergative, hd_imp_c only  
<ex>Begone!
"""
  [ SYNSEM unerg_imp_verb ].

v_-_bse_le := main_verb & 
"""
No cmps, base form only          
<ex>That will do.
"""
  [ SYNSEM unerg_bse_verb ].

v_np_poss_le := main_verb & 
"""
Cmps NP, only have               
<ex>B has C.
"""
  [ SYNSEM poss_verb ].

v_np_poss-cx_le := main_verb & 
"""
Cmps NP, only contracted have (no extraction)
<ex>B has C.
"""
  [ SYNSEM poss_verb &
	   [ LOCAL [ CAT [ HEAD.TAM.TENSE present,
			   VAL.COMPS < canonical_synsem > ],
		     AGR.PNG.PN -3s ] ] ].

;; DPF 19-feb-10 - Prevent "got there" via COMPS..NORM no_rel.
;; DPF 07-apr-10 - No longer remember why we have this, given the ordinary
;; NP-complement get_v2.  It does not appear in any of the treebanked data,
;; so let's try living without it, to avoid spurious ambiguity for e.g.
;; "he has got cats"

v_np_got_le := norm_nonconj_word &
"""
Cmps NP, only got                
<ex>B got C.
"""
  [ SYNSEM poss_got_verb ].
			  
v_cp_rkey-bse_le := v_cp_prop_raise_key_lexent &
"""
Cmps S, embedded subj tag, base  
<ex>I'll suppose C can, can he
"""
  [ SYNSEM.LOCAL bse_verb ].

v_cp_rkey-3sg_le := v_cp_prop_raise_key_lexent &
"""
Cmps S, embedded subj tag, 3sing 
<ex>B supposes C can, can't he
"""
  [ SYNSEM cp_prop_raise_key_3sg_verb ].

v_cp_rkey-n3sg_le := v_cp_prop_raise_key_lexent &
"""
Cmps S, embedded subj tag, non3s 
<ex>We suppose C can, can't he
"""
  [ SYNSEM cp_prop_raise_key_n3sg_verb ].

v_cp_rkey-pst_le := v_cp_prop_raise_key_lexent &
"""
Cmps S, embedded subj tag, past  
<ex>I supposed C is, isn't he
"""
  [ SYNSEM cp_prop_raise_key_pst_verb ].

v_cp_rkey-prp_le := v_cp_prop_raise_key_lexent &
"""
Cmps S, embedded subj tag, prp   
<ex>I'm supposing C is, is he
"""
  [ SYNSEM cp_prop_raise_key_prp_verb ].

v_cp_rkey-psp_le := v_cp_prop_raise_key_lexent &
"""
Cmps S, embedded subj tag, psp   
<ex>I've supposed C is, is he
"""
  [ SYNSEM cp_prop_raise_key_psp_verb ].

v_adv_le := norm_basic_main_verb &
"""
Cmps Adv-phr                     
<ex>B did badly.
"""
  [ SYNSEM adv_verb,
    INFLECTD - ].

v_np-adv_le := norm_basic_main_verb &
"""
Cmps NP, Adv-phr                     
<ex>B put it badly.
"""
  [ SYNSEM np_adv_verb,
    INFLECTD - ].

; 'made': "Kim made sandy arrive' but "Sandy was made TO arrive'
v_vp_sorb-psv_le := norm_basic_main_verb &
"""
Cmps VP(inf), psv, s-o-rais      
<ex>B was made to arrive.
"""
  [ SYNSEM sor_psv_verb,
    INFLECTD - ].

; 'faded'
v_-_psv_le := norm_basic_main_verb &
"""
No cmps, psv                     
<ex>B was faded.
"""
  [ SYNSEM passive_unerg_synsem,
    INFLECTD + ].

; 'born'
v_-_psv-prd_le := norm_basic_main_verb &
"""
No cmps, psv, predicative only
<ex>B was born.
<nex>The born child cried.
"""
  [ INFLECTD +,
    SYNSEM passive_unerg_synsem,
    ALTS.ATTRJ - ].

; 'surrounded'
v_-_psv-with_le := norm_basic_main_verb &
"""
No cmps, psv, predicative only, oblig with-PP
<ex>B was born.
<nex>The city was surrounded with mountains.
"""
  [ INFLECTD +,
    SYNSEM passive_unerg_with_synsem,
    ALTS.ATTRJ - ].

;; DPF 2016-09-26 - Removed NORM norm_rel because we want 
;; *the Chicago-based team* where the n-v_j-cpd_c rule requires its second dtr
;; to be NORM no_rel.  Maybe FIX whatever that constraint was doing.
;; 
; 'faded'
v_-_psv-att_le := norm_basic_main_verb &
"""
No cmps, psv, attributive only
<ex>The flopped issue disappeared.
"""
  [ SYNSEM passive_unerg_synsem &
	   [ LOCAL.CAT [ VAL.SUBJ < canonical_synsem >,
			 POSTHD - ],
	     NONLOC.SLASH 0-dlist ],
    INFLECTD + ].

; 'horseback riding'
v_-_prp_le := norm_basic_main_verb &
"""
No cmps, prp only
<ex>B went horseback riding
"""
  [ SYNSEM unerg_prp_verb,
    INFLECTD + ].

;; 'put' - "That was well put", but block in n-v compounds like "*idea-put"
;; Also block depictives via OPT -
;;
v_-_psv-ncnh_le := norm_basic_main_verb &
"""
No cmps, psv, no n-v-ed cmpnd    
<ex>That was put clearly.
"""
  [ INFLECTD +,
    SYNSEM passive_unerg_synsem &
	   [ LOCAL.CAT.HEAD [ MINORS.NORM norm_rel ],
	     OPT - ] ].

; 'unevaluated (by Abrams)'
v_-_psv-neg_le := main_verb_newltop &
"""
No cmps, psv, neg semantics      
<ex>B was unevaluated.
"""
  [ SYNSEM passive_unerg_neg_synsem,
    INFLECTD + ].

v_np_thr-s_le := main_verb & 
"""
Cmps NP, expl-there subj         
<ex>There existed many dogs.
"""
  [ SYNSEM there_verb_nohc_synsem ].

v_np_locinv-mv_le := loc_inv_mv_word
"""
Cmps NP, loc inversion           
<ex>In the room stood a clock.
"""
.

v_np_locinv-is_le := loc_inv_be_word &
"""
Cmps NP, loc inversion, aux is   
<ex>In the room is a clock.
"""
  [ SYNSEM.LOCAL [ CAT.HEAD.TAM.TENSE present,
                   AGR.PNG.PN 3s ] ].

v_np_locinv-are_le := loc_inv_be_word &
"""
Cmps NP, loc inversion, aux are  
<ex>In the room are clocks.
"""
  [ SYNSEM.LOCAL [ CAT.HEAD.TAM.TENSE present,
                   AGR.PNG.PN 3p ] ].

v_np_locinv-was_le := loc_inv_be_word &
"""
Cmps NP, loc inversion, aux was  
<ex>In the room was a clock.
"""
  [ SYNSEM.LOCAL [ CAT.HEAD.TAM.TENSE past,
                   AGR.PNG.PN 3s ] ].

v_np_locinv-were_le := loc_inv_be_word &
"""
Cmps NP, loc inversion, aux were 
<ex>In the room were clocks.
"""
  [ SYNSEM.LOCAL [ CAT.HEAD.TAM.TENSE past,
                   AGR.PNG.PN 3p ] ].

v_np_locinv-be_le := basic_loc_inv_be_word &
"""
Cmps NP, loc inversion, aux be 
<ex>In the room will be clocks.
"""
  [ SYNSEM.LOCAL.CAT.HEAD [ VFORM bse,
			    TAM.TENSE no_tense ] ].

;; May he always be blessed
;;
v_np-vp_imp_le := main_verb_mliszt & 
"""
Cmps NP,VP(bse), imperative only
<ex>May he always be blessed
<ex>May there be peace on earth
"""
  [ SYNSEM sorb_verb &
	   [ LOCAL.CAT.HEAD.VFORM imp_vform ] ].

;; DPF 2018-08-24 - For some reason, PET's flop will not compile this file
;; without the following 99-character line preceded by a semicolon:
;; (and similarly for auxverbs.tdl but not lextypes.tdl)
;xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
